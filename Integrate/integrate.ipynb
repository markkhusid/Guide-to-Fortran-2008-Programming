{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "This program illustrates the use of modules within Fortran programs.  The first module contains a numerical integration function that integrates using approximate integration by the sum of trapezoids.  The second module contains the functions that are to be integrated.  The original example in the book contained just the square function.  I later added the cubed function for fun.  The function that are integrated are x^2, and X^3.  The limits of integration are 0 to 1, and the number of trapezoids is set to 100.  The program makes use of a module to define the function to integrate, and then this function acts a parameter for the integrate function.  The function interface is defined in the integrate_module."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "The integral of x^2 from     0.0000 to     1.0000 =     0.3333\n",
      "\n",
      "The integral of x^3 from     0.0000 to     1.0000 =     0.2500\n",
      "\n"
     ]
    }
   ],
   "source": [
    "module integrate_module\n",
    "\n",
    "    implicit none\n",
    "    private\n",
    "    public              :: integral\n",
    "    \n",
    "contains\n",
    "\n",
    "    function integral(f, a, b, n) result (integral_result)\n",
    "        ! Calculates a trapezoidal approximation to an area using n trapezoids\n",
    "        ! The region is bounded by lines x=a, y=0, x=b, and the curve y=f(x)\n",
    "        \n",
    "        interface\n",
    "            function f(x) result (f_result)\n",
    "                real, intent(in)    :: x\n",
    "                real                :: f_result\n",
    "            end function f\n",
    "        end interface\n",
    "        \n",
    "        real, intent(in)            :: a, b\n",
    "        integer, intent(in)         :: n\n",
    "        real                        :: integral_result\n",
    "        real                        :: h, total\n",
    "        integer                     :: i\n",
    "        \n",
    "        h = (b - a) / n\n",
    "        \n",
    "        ! Calculate the sum f(a)/2+f(a+h)+...+f(b-h)+f(b)/2\n",
    "        ! Do the first and last terms first\n",
    "        total = 0.5 * (f(a) + f(b))\n",
    "        do i = 1, n - 1\n",
    "            total = total + f(a + i * h)\n",
    "        end do\n",
    "        \n",
    "        integral_result = h * total\n",
    "    end function integral\n",
    "    \n",
    "end module integrate_module\n",
    "\n",
    "module function_module\n",
    "    implicit none\n",
    "    private\n",
    "    public                      :: square\n",
    "    public                      :: cube\n",
    "    \n",
    "contains\n",
    "    function square(x) result (squared_x)\n",
    "    \n",
    "        real, intent(in)        :: x\n",
    "        real                    :: squared_x\n",
    "        \n",
    "        squared_x = x ** 2\n",
    "    \n",
    "    end function square\n",
    "    \n",
    "    function cube(x) result (cubed_x)\n",
    "        \n",
    "        real, intent(in)        :: x\n",
    "        real                    :: cubed_x\n",
    "        \n",
    "        cubed_x = x ** 3\n",
    "        \n",
    "    end function cube\n",
    "    \n",
    "end module function_module\n",
    "\n",
    "program integrate\n",
    "    \n",
    "    use integrate_module\n",
    "    use function_module\n",
    "    \n",
    "    implicit none\n",
    "    \n",
    "    real    :: start, finish, answer\n",
    "    integer :: num_intervals\n",
    "    \n",
    "    start = 0.0\n",
    "    finish = 1.0\n",
    "    num_intervals = 100\n",
    "    \n",
    "    answer = integral(square, start, finish, num_intervals)\n",
    "    write (*, '(a, f10.4, a, f10.4, a, f10.4)') \"The integral of x^2 from \", start, \" to \", finish, \" = \", answer\n",
    "    print *\n",
    "    \n",
    "    start = 0.0\n",
    "    finish = 1.0\n",
    "    num_intervals = 100\n",
    "    \n",
    "    answer = integral(cube, start, finish, num_intervals)\n",
    "    write (*, '(a, f10.4, a, f10.4, a, f10.4)') \"The integral of x^3 from \", start, \" to \", finish, \" = \", answer\n",
    "    print *\n",
    "    \n",
    "end program integrate"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "gfortran",
   "language": "Fortran",
   "name": "gfort_spec"
  },
  "language_info": {
   "file_extension": "f90",
   "mimetype": "text/plain",
   "name": "fortran"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
